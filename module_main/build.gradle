plugins {
    id 'org.jetbrains.kotlin.android'
}

if(isRelease){
    apply plugin: 'com.android.library'
}else{
    apply plugin: 'com.android.application'
}

android {
    compileSdk app_android.compileSdk

    defaultConfig {
        if(!isRelease){
            applicationId app_applicationId.module_main
        }
        minSdk app_android.minSdk
        targetSdk app_android.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    //源集 - 用来设定java目录或者资源目录
    sourceSets{
        main{
            if(!isRelease){
                //组件化模式
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            }else{
                //集成模式
                manifest.srcFile 'src/main/AndroidManifest.xml'

                //排除掉debug包得信息
                java{
                    exclude '**/debug/**'
                }
            }
        }
    }
}

dependencies {

    implementation "androidx.core:core-ktx:$versions.androidCoreVersion"
    implementation "androidx.appcompat:appcompat:$versions.appcompatVersion"
    implementation "com.google.android.material:material:$versions.materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayoutVersion"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}